plugins {
    id 'fabric-loom' version '0.5-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id "com.peterabeles.gversion" version "1.8.3"
}

gversion {
  srcDir = "src/main/java/"
  classPackage = "com.javazilla.bukkitfabric"
  className = "GitVersion"
}
project.compileJava.dependsOn(createVersionFile)

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

configurations { 
    extraLibs
    removeLibs
}

minecraft {
    accessWidener = file("src/main/resources/bukkitfabric.accesswidener")
}

repositories {
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    maven { url = 'https://repo.codemc.io/repository/maven-releases/' }
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Spigot API
    extraLibs fileTree(dir: 'libs', include: 'paper-api-1.16.4-416.jar')
    extraLibs fileTree(dir: 'libs', include: 'commons-lang-2.6.jar')
    extraLibs fileTree(dir: 'libs', include: 'slf4j.jar')
	extraLibs fileTree(dir: 'libs', include: 'jline-3.19.jar')
    compile   fileTree(dir: 'libs', include: 'material-compile-only.jar')
    extraLibs group: "org.jetbrains", name: "annotations", version: "18.0.0"
    extraLibs group: "com.googlecode.json-simple", name: "json-simple", version: "1.1.1"
    extraLibs "org.yaml:snakeyaml:1.27"

    // NMS Remapping
    extraLibs group: "com.github.IsaiahPatton", name: "SpecialSource", version: "31af248ea6"
    extraLibs group: "com.github.OrionMinecraft", name: "SrgLib", version: "master-SNAPSHOT"

    // (Optional) Fabric Permissions API.
    modImplementation "com.javazilla.mods:permissions:1.1"

    // Remove parts that are included with Minecraft or Downloaded
    removeLibs group: "com.google.guava", name: "guava", version: "21.0"
    removeLibs group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    removeLibs group: 'org.apache.commons', name: 'commons-collections4', version: '4.2'
    removeLibs group: 'commons-collections', name: 'commons-collections', version: '3.2.2'
    removeLibs group: 'commons-lang', name: 'commons-lang', version: '2.6'
    removeLibs group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    configurations.compile.extendsFrom(configurations.extraLibs)
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        if(System.env.BUILD_NUMBER){
            expand "version": System.env.BUILD_NUMBER
        }
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) { options.encoding = "UTF-8" }

jar {
    from "LICENSE"
    from {
        (configurations.extraLibs - configurations.removeLibs).filter{ it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude("**/mojang-translations/")
}

shadowJar {
    configurations = [project.configurations.compile]
    dependencies {
      exclude(dependency('com.mojang:minecraft:*'))
      exclude(dependency('org.objectweb:asm:*'))
      exclude(dependency('com.google.guava:guava:*'))
    }
}
